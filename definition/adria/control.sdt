
/*
 * multipurpose code block
 */

block {
    entry -> "{" -> "}" -> return
    "{" -> statement:statement -> "}"
    statement:statement -> statement:statement
}

/*
 * break/return/continue
 */

return_statement {
    entry -> "return":type -> return
    "return":type -> literal_expression:value -> return
}

flow_statement {
    entry -> "break":type -> return
    entry -> "continue":type -> return
}

/*
 * if...else if...else block
 */

if_conditional {
    entry -> "if" -> "(" -> expression:condition -> ")" -> block:body -> return
}

if_unconditional {
    entry -> block:body -> return
}

if_statement {

    entry -> if_conditional:if -> return
    if_conditional:if -> "else"[if_to_elseif] -> if_conditional:else_if -> return
    if_conditional:else_if -> "else"[elseif_to_elseif] -> if_conditional:else_if

    if_conditional:if -> "else"[if_to_else] -> if_unconditional:else -> return
    if_conditional:else_if -> "else"[elseif_to_else] -> if_unconditional:else
}

/*
 * while
 */

do_while_statement {
    entry -> "do" -> block:body -> "while" -> "(" -> expression:condition -> ")" -> return
}

while_statement {
    entry -> "while" -> "(" -> expression:condition -> ")" -> block:body -> return
}

/*
 * for
 */

for_count_init {
    entry -> var_def:init -> return
    entry -> expression:init -> return
}

for_count_statement {
    entry -> "for" -> "(" -> for_count_init -> t -> expression:test[full] -> t -> expression:cond_op -> ")" -> block:body -> return
    "(" -> expression:test[short] -> ")"
}

/*
 * for ([var] key[, value] in source) { ... }
 */

for_in_statement {
    entry -> "for" -> "(" -> ident:key -> "in" -> expression:source -> ")" -> block:body -> return
    "(" -> "var":var -> ident:key
    ident:key -> "," -> ident:value -> "in"
}

/*
 * switch statement !todo scoping how?
 */

switch_case {
    entry -> "case" -> expression:match -> ":" -> switch_block:body -> return
    ":" -> return
}

switch_default {
    entry -> "default" -> ":" -> switch_block:body -> return
}

switch_block {
    entry -> statement:statement -> return
    statement:statement -> statement:statement
}

switch_statement {
    entry -> "switch" -> "(" -> expression:value -> ")" -> "{"
    "{" -> switch_case:case -> "}"
    switch_case:case -> switch_case:case
    switch_case:case -> switch_default:default -> "}"
    "}" -> return
}

/*
 * throw try catch finally
 */

throw_statement {
    entry -> "throw" -> expression:exception -> return
}

try {
    entry -> "try" -> block:body -> return
}

catch_specific {
    entry -> "catch" -> "(" -> expression:type -> ident:value -> ")" -> block:body -> return
}

catch_all {
    entry -> "catch" -> "(" -> ident:value -> ")" -> block:body -> return
}

catch {
    entry -> catch_all:all -> return
    entry -> catch_specific:specific -> return
    catch_specific:specific -> catch_specific:specific
    catch_specific:specific -> catch_all:all
}

finally {
    entry -> "finally" -> block:body -> return
}

try_catch_finally_statement {
    entry -> try:try -> catch:catch -> return
    catch:catch -> finally:finally -> return
}
