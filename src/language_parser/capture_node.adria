/*
 * Adria transcompiler
 *
 * Copyright (C) 2014 Dennis MÃ¶hlmann <mail@dennismoehlmann.de>
 * Licensed under the MIT license.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of
 * this software and associated documentation files (the "Software"), to deal in
 * the Software without restriction, including without limitation the rights to
 * use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of
 * the Software, and to permit persons to whom the Software is furnished to do so,
 * subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
 * FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
 * COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
 * IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
 * CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */
var SourceNode = require('../source_node');

/**
 * An abstract syntax node generated by CaptureNode::fromResults()
 */
module CaptureNode = proto {

    parser  : null,
    parent  : null,
    children: null,
    key     : '',
    value   : '',
    row     : 0,
    col     : 0,

    /**
     * construct new capture node for given key/value
     *
     * @param string key
     * @param string value
     */
    constructor: func(string key, string value) {
        this.key    = key;
        this.value  = value;
    },

    /**
     * convert to JSON, loses parent
     */
    toJSON: func() {

        // recursively include all children

        var children = [];

        for (var id in this.children) {
            children.push(this.children[id].toJSON());
        }

        // try to keep it small

        return {
            _: this.constructor.name,
            s: children,
            k: this.key,
            v: this.value,
            r: this.row,
            c: this.col
        };
    },

    /**
     * prototype method, create from JSON, restores parent
     *
     * @param object decoded JSON
     * @param object parentNode initial parent, the parser for non-recursive call
     * @param func typeMapper
     */
    fromJSON: func(object json, object parentNode, func typeMapper) {

        var Type = typeMapper('', json._);

        var result = new Type(json.k, json.v);
        result.parser = parentNode instanceof CaptureNode ? parentNode.parser : parentNode;
        result.parent = parentNode;
        result.row = json.r;
        result.col = json.c;
        result.children = [ ];

        var jsonChildren = json.s;
        var resultChildren = result.children;

        for (var id in jsonChildren) {
            resultChildren.push(CaptureNode::fromJSON(jsonChildren[id], result, typeMapper));
        }

        return result;
    },

    /**
     * static, converts parser results to syntax tree structure
     *
     * @param Array results array of GeneratorState
     * @param object parser root node parent, the parser
     * @return CaptureNode root node of the capture tree
     */
    fromResults: func(Array results, object parser, typeMapper) {

        var InitialType = parser.mapType('', '');
        var root = new InitialType('', '');
        var current = root;
        var lastStack = [];
        var result, stack, diff, node;

        // set parent parser for root node

        root.parser = parser;
        root.parent = parser;

        // process each result

        for (var resultId in results) {
            result = results[resultId];
            stack = result.stack;

            // diff stack against previous stack

            diff = stackDiff(stack, lastStack, result.minStack);

            // ascend as far as required

            while (diff.ascend--) {
                current = current.parent;
            }

            // attach new nodes

            for (var nodeId in diff.create) {
                node = diff.create[nodeId];
                current = current.addNew(node.capture, node.name, typeMapper(node.capture, node.name));
                current.row = result.token.pos.row;
                current.col = result.token.pos.col;
            }

            // attach the actual match

            node = result.node;

            if (node.capture !== '') {
                var match = current.addNew(node.capture, result.token.data, typeMapper(node.capture, node.name));
                match.row = result.token.pos.row;
                match.col = result.token.pos.col;
            }

            // lastStack for the next token

            lastStack = stack;
        }

        return root;
    },

    /**
     * adds given CaptureNode to this node and set's its parent
     *
     * @param CaptureNode child new node to add
     * @return CaptureNode given CaptureNode
     */
    add: func(CaptureNode child) {

        if (this.children == null) {
            this.children = [ ];
        }

        child.parser = this.parser;
        child.parent = this;
        this.children.push(child);
        return child;
    },

    /**
     * creates and adds a node of given subclass to this node
     *
     * @param string key node key
     * @param string value node value
     * @param func Constructor constructor to use for the new node
     * @return CaptureNode
     */
    addNew: func(string key, string value, func Constructor) {
        return this.add(new Constructor(key, value));
    },

    /**
     * returns true if node is not a dummy
     *
     * @return boolean
     */
    isNode: func() {
        return this.col !== -1;
    },

    /**
     * returns true if node is a dummy
     *
     * @return boolean
     */
    isDummy: func() {
        return this.col === -1;
    },

    /**
     * returns true if node has no children
     *
     * @return boolean
     */
    isLeaf: func() {
        return (this.children instanceof Array === false);
    },

    /**
     * returns true if node has children
     *
     * @return boolean
     */
    isBranch: func() {
        return (this.children instanceof Array);
    },

    /**
     * returns number of child-nodes
     *
     * @return finite
     */
    length: prop {
        get: func() {
            return (this.children instanceof Array ? this.children.length : 0);
        },
    },

    /**
     * returns number of parents above node
     *
     * @return integer depth
     */
    depth: func() {

        var result = 0;
        var current = this;

        while (current.parent instanceof CaptureNode) {

            result += 1;
            current = current.parent;
        }

        return result;
    },

    /**
     * returns ancestor with matching key and/or value
     *
     * @param string/array key
     * @param string/array value
     * @param any dummy result when ancestor is not found
     * @return CaptureNode/dummy
     */
    ancestor: func(key, value = null, dummy = this.dummy) {

        var current = this;

        key = typeof key === 'string' ? [ key ] : key;
        value = typeof value === 'string' ? [ value ] : value;

        if (key !== null && value !== null) {
            while (current.parent instanceof CaptureNode && key.indexOf(current.parent.key) === -1 && value.indexOf(current.parent.value) === -1) {
                current = current.parent;
            }
        } else if (key !== null) {
            while (current.parent instanceof CaptureNode && key.indexOf(current.parent.key) === -1) {
                current = current.parent;
            }
        } else if (value !== null) {
            while (current.parent instanceof CaptureNode && value.indexOf(current.parent.value) === -1) {
                current = current.parent;
            }
        }

        if (current.parent instanceof CaptureNode) {
            return current.parent;
        } else {
            return dummy;
        }
    },

    /**
     * finds given constructor in ancestors
     *
     * @param func Constructor prototype constructor to find
     * @param func StopConstructor prototype constructor to stop searching at (will be returned if also instance of Constructor)
     * @param boolean fromParent default true, will also check this if false
     * @param any dummy result when constructor is not found
     * @return CaptureNode/dummy
     */
    findProto: func(func Constructor, func? StopConstructor = null, boolean fromParent = true, dummy = this.dummy) {

        var current = fromParent ? this.parent : this;

        while (current instanceof CaptureNode && current instanceof Constructor === false && (StopConstructor === null || current instanceof StopConstructor === false)) {
            current = current.parent;
        }

        return current instanceof Constructor ? current : dummy;
    },

    /**
     * get the <index>th node that matches key with index = 0 being the first node
     *
     * @param string key node key to find
     * @param number index which of multiple key-matching nodes
     * @param any dummy result when ancestor is not found
     * @return CaptureNode/dummy
     */
    get: func(string key, number index = 0, dummy = this.dummy) {

        if (this.children instanceof Array) {

            for (var id in this.children) {
                var child = this.children[id];

                if (child.key == key && index-- == 0) {
                    return child;
                }
            }
        }

        return dummy;
    },

    /**
     * check if node has a child with given key
     *
     * @param string key node key to find
     * @return boolean
     */
    has: func(string key) {

        if (this.children instanceof Array) {

            for (var id, child in this.children) {

                if (child.key == key) {
                    return true;
                }
            }
        }

        return false;
    },

    /**
     * iterate over all child-nodes
     *
     * @param func callback function, func(Node node, boolean first, boolean last)
     */
    each: func(func fn) {

        var children = this.children;

        if (children instanceof Array) {

            var last = children.length -1;

            for (var id in children) {
                fn.call(this, children[id], +id === 0, +id === last);
            }
        }
    },

    /**
     * iterate over all child-nodes with given key. can also iterate over nested
     * child nodes by passing a . separated path, i.e. function.parameter_list.item
     * for iterate over all item-nodes
     *
     * @param string key node key or . separated path
     * @param func callback function, func(node, first, last)
     */
    eachKey: func(string key, func fn) {

        var part = key.split('.');

        if (this.children instanceof Array) {

            var children = this.children;
            var len = children.length;
            var prevChild = null;
            var first = true;
            var id;

            for (id = 0; id < len; id++) {
                var child = children[id];

                if (child.key === part[0]) {
                    if (part.length === 1) {

                        // delay by one loop to be able to identify the last item

                        if (prevChild !== null) {
                            fn.call(this, prevChild, first, false);
                            first = false;
                        }

                        prevChild = child;

                    } else if (part.length > 1) {
                        child.eachKey(part.slice(1).join('.'), fn);
                    }
                }
            }

            // last item

            if (prevChild !== null && prevChild.key === part[0] && part.length === 1) {
                fn.call(this, prevChild, first, true);
            }
        }
    },

    /**
     * chains multiple get calls from input path, split by splitter, id indicated
     * in array notation, i.e. this.path('value.function[3].body')
     *
     * @param string pathString
     * @param string splitter path separator
     * @param any dummy result when ancestor is not found
     * @return CaptureNode/dummy
     */
    path: func(string pathString, string splitter = '.', dummy = this.dummy) {

        var step;
        var current = this;

        pathString = pathString.split(splitter);

        for (var id in pathString) {
            step = pathString[id].split('[');

            if (step.length === 1) {
                current = current.get(step[0]);
            } else {
                current = current.get(step[0], parseInt(step[1].slice(0, -1)));
            }

            if (current.isDummy()) {
                return dummy;
            }
        }

        return current;
    },

    /**
      * extracts a set of this node's children and returns them
      *
      * @param finite from start inclusive child index
      * @param finite to end inclusive child index
      */
    extract: func(finite from, finite to) {

        return this.children.splice(from, to - from + 1);
    },

    /**
      * moves a set of this node's children into a single child node and places it where the set used to be
      *
      * @param finite from inclusive start child index
      * @param finite to inclusive end child index
      * @param Constructor optional constructor for the new node, defaults to parent type
      */
    nest: func(finite from, finite to, Constructor = this.constructor) {

        var node = new Constructor(this.key, this.value);

        node.children = this.children.splice(from, to - from + 1, node);
        node.parent = this;
        node.row = node.children[0].row;
        node.col = node.children[0].col;

        for (var id, child in node.children) {
            child.parent = node;
        }
    },

    /**
     * returns a newline and optionally increases or decreases indentation for the following lines
     *
     * @param finite indent change in indentation
     * @param SourceNode node optionally a (previous) node to trim
     */
    nl: func(finite indent = 0, SourceNode? node = null) {

        this.parser.indent += indent;

        if (node !== null) {
            node.trim();
        }

        return '\n' + String.repeat(this.parser.indent * 4, ' ');
    },

    /**
     * creates and returns a new SourceNode based on CaptureNode's position and given code
     *
     * @param any code SourceNode or string
     */
    csn: func(code) {

        return new SourceNode(this.row, this.col -1, null, code); //!todo why is everthing offset by one if I don't subtract 1 here? col start at 0, row at 1? unlikely
    },

    /**
     * returns node location in source, useful for error output
     *
     * @return string
     */
    loc: func() {
        return ' in $0 line $1, column $2'.format(this.parser.file, this.row, this.col);
    },

    /**
     * recursively calls children for preprocessing purposes
     *
     * @param any state user-defined object passed to every child
     */
    preprocess: func(state) {

        if (this.children instanceof Array) {
            for (var id, child in this.children) {
                child.preprocess(state);
            }
        }
    },

    /**
     * default node to string handler, returns empty string but calls children recursively
     *
     * @return string
     */
    toString: func() {

        var result = '';

        if (this.children instanceof Array) {
            for (var id in this.children) {
                result += this.children[id].toString();
            }
        }

        return result;
    },

    /**
     * default node to source-node handler, returns empty node but calls children recursively
     *
     * @return SourceNode
     */
    toSourceNode: func() {

        var result = new SourceNode(null, null);

        if (this.children instanceof Array) {
            for (var id in this.children) {
                var child = this.children[id].toSourceNode();
                if (child !== '') {
                    result.add(child);
                }
            }
        }

        return result;
    },

    /**
     * recursively calls children for pre-scanning purposes
     *
     * @param any state user-defined object passed to every child
     */
    prescan: func(state) {

        if (this.children instanceof Array) {
            for (var id, child in this.children) {
                child.prescan(state);
            }
        }
    },

    /**
     * recursively calls children for scanning purposes
     *
     * @param any state user-defined object passed to every child
     */
    scan: func(state) {

        if (this.children instanceof Array) {
            for (var id, child in this.children) {
                child.scan(state);
            }
        }
    },

    /**
     * recursively calls children for reset purposes
     *
     * @param any state user-defined object passed to every child
     */
    reset: func(state) {

        if (this.children instanceof Array) {
            for (var id, child in this.children) {
                child.reset(state);
            }
        }
    },
};

/**
 * dummy returned by some functions as a convenience
 */
CaptureNode::dummy = new CaptureNode('', '');
CaptureNode::dummy.row = -1;
CaptureNode::dummy.col = -1;

/**
 * compares two stacks and returns number of parent-acends and list of capture desends to get from
 * lastStack to stack. internally used by fromResults
 *
 * @param Array stack current stack
 * @param Array lastStack stack at last processed token
 * @param finite minStackLen the minimum size the stack had between the tokens for stack and lastStack
 */
var stackDiff = func(Array stack, Array lastStack, finite minStackLen) {

    // find deepest capture node that is present in both current and last stack

    var deepestCommonCapture = -1;
    var minLen = Math.min(stack.length, lastStack.length, minStackLen);

    for (var i = 0; i < minLen; i++) {
        if (stack[i].node === lastStack[i].node) {
            if (stack[i].node.capture !== '') {
                deepestCommonCapture = i;
            }
        } else {
            break;
        }
    }

    // find number of captures between deepest common capture and deepest previous capture
    // this is the number of nodes fromResults() needs to ascend before attaching new capture nodes

    var numCaptures = 0;
    var lastLen = lastStack.length;

    for (var i = deepestCommonCapture + 1; i < lastLen; i++) {
        if (lastStack[i].node.capture !== '') {
            numCaptures++;
        }
    }

    // find all capture nodes below the deepest common capture
    // fromResults() will have to create capture nodes for these

    var captures = [];
    var len = stack.length;

    for (var i = deepestCommonCapture + 1; i < len; i++) {
        if (stack[i].node.capture !== '') {
            captures.push(stack[i].node);
        }
    }

    return { ascend: numCaptures, create: captures };
};